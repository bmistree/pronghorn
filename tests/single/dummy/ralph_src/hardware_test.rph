Struct RoutingTableEntry alias 'pronghorn.RTable.RoutingTableEntry'
{
    TVar TrueFalse active;
    // ipv4 addresses specified as a.b.c.d
    TVar Text src_ip;
    TVar Text dst_ip;
    // -1 means that should drop the packet
    TVar Number action;
}

Struct Switch alias 'pronghorn.SwitchJava.Switch'
{
    // unique id for each switch
    TVar Text switch_id;    
    TVar Number available_capacity;
    TVar List (element: Struct RoutingTableEntry) rtable;
}

Endpoint PronghornInstance
{
    TVar Map(from: Text, to: Struct Switch) instance_switches;

    add_switch(Struct Switch new_switch)
    {
        Text switch_id = new_switch.switch_id;
	instance_switches.set(switch_id,new_switch);
    }

    switch_failure(Text switch_id)
    {
	instance_switches.remove(switch_id);
    }

    add_rtable_entry(
        Text switch_id,TrueFalse active,Text src_ip, Text dst_ip,Number action)
    {

        Struct RoutingTableEntry entry;
        entry.active = active;
        entry.src_ip = src_ip;
        entry.dst_ip = dst_ip;
        entry.action = action;

        atomically
        {            
            Struct Switch switch_to_add_to = instance_switches.get(switch_id);
            switch_to_add_to.rtable.append(entry);
        }
    }

    rtable_size(Text switch_id) returns Number
    {
        Struct Switch target_switch = instance_switches.get(switch_id);
        return target_switch.rtable.size();
    }
        
    
}
